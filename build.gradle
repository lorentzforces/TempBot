plugins {
	id "java"
	id "application"
}

apply plugin: 'java'
apply plugin: 'application'

// this var for application plugin run task
mainClassName = "tempbot.Bot"

repositories {
	mavenCentral()
}

dependencies {
	implementation "com.discord4j:discord4j-core:3.1.0"
	implementation "org.snakeyaml:snakeyaml-engine:2.3+"
	implementation "org.apache.logging.log4j:log4j-api:2.11.+"
	implementation "org.apache.logging.log4j:log4j-core:2.11.+"
	implementation "org.apache.logging.log4j:log4j-slf4j-impl:2.11.+"
	testImplementation "junit:junit-dep:4.11"
}


compileJava {
	sourceCompatibility = 11
	targetCompatibility = 11
}

test {
	testLogging {
		// always run all tests even when nothing's changed
		outputs.upToDateWhen { false }
		// actually show messages from tests (sigh)
		showStandardStreams = true

		// TODO: make this nicer
		// print summary stats of test runs
		afterSuite { desc, result ->
			if (!desc.parent) { // will match the outermost suite
				println "tests passed: ${result.successfulTestCount}"
				println "tests failed: ${result.failedTestCount}"
				if (result.skippedTestCount > 0) {
					println "tests skipped: ${result.skippedTestCount}"
				}
			}
		}
	}
}

jar {
	destinationDirectory = file("$rootDir")
	manifest {
		attributes "Main-Class": mainClassName
		attributes "Multi-Release": true
	}

	from configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
}
